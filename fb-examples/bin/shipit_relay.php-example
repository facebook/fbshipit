<?hh
/**
 * Copyright (c) 2016-present, Facebook, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree. An additional grant
 * of patent rights can be found in the PATENTS file in the same directory.
 */
namespace Facebook\ShipIt;

require_once(__DIR__.'/../autoload.php');

final class ShipItRelay extends FBShipItCLI {
  const FBRK_ROOT = 'fbobjc/Libraries/FBReactKit/';
  const RELAY_OSS = self::FBRK_ROOT.'js/RKJSModules/Libraries/Relay/oss/';

  // Open source modules.
  const GITHUB_ROOT = self::RELAY_OSS.'__github__/';
  const BABEL_PLUGIN_RELAY = self::RELAY_OSS.'babel-plugin-relay/';
  const REACT_RELAY = self::RELAY_OSS.'react-relay/';
  const RELAY_COMPILER = self::RELAY_OSS.'relay-compiler/';
  const RELAY_RUNTIME = self::RELAY_OSS.'relay-runtime/';

  // Directories which will be removed in the future.
  const NEW_PLUGIN_ROOT =
    self::FBRK_ROOT.'js/RKJSModules/Libraries/Relay/babel-plugin-graphql/';
  const RELAY_ROOT = self::FBRK_ROOT.'js/RKJSModules/Libraries/Relay/relay/';
  const PLUGIN_ROOT =
    self::FBRK_ROOT.'js/server/fb-transforms/babel-relay-plugin/';

  <<__Override>>
  protected static function projectFilterChangeset(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return $changeset
      |> FBCommonFilters::stripSummariesByDefault($$)
      |> FBCommonFilters::applyAll($$)
      |> ShipItPathFilters::stripPaths(
          $$,
          ImmVector {
            // Don't include the README in the top directory.
            '@^'.self::RELAY_OSS.'README.md@',
            // Only sync out files from the Relay project
            // Ignore __forks__ (but permit __github__/__forks__/)
            '@^'.self::RELAY_OSS.'(?!__github__).+?/__forks__/@',
            '@^'.self::RELAY_ROOT.'__forks__/@',
            // The public project uses different Flow configs than we do internally
            '@^'.self::RELAY_ROOT.'\.flowconfig$@',
            // The yarn.lock uses the offline-cache internally which makes it
            // useless externally.
            '@/yarn\.lock$@',
          },
        )
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> preg_replace('@/\.github\.flowconfig$@',
            '/.flowconfig', $path),
        )
      |> ShipItPathFilters::moveDirectories(
        $$,
        static::getPathMappings(),
      );
  }

  <<__Override>>
  public static function getPathMappings(): ImmMap<string, string> {
    return ImmMap {
      // Special cases
      self::GITHUB_ROOT.'eslintrc' => '.eslintrc',
      self::GITHUB_ROOT.'src/flowconfig' => 'src/.flowconfig',

      // Modules
      self::GITHUB_ROOT => '',
      self::BABEL_PLUGIN_RELAY => 'packages/babel-plugin-relay/',
      self::REACT_RELAY => 'packages/react-relay/',
      self::RELAY_COMPILER => 'packages/relay-compiler/',
      self::RELAY_RUNTIME => 'packages/relay-runtime/',

      // Directories which will be removed in the future.
      self::NEW_PLUGIN_ROOT => 'src/babel-plugin-graphql/',
      self::PLUGIN_ROOT => 'scripts/babel-relay-plugin/',
      self::RELAY_ROOT.'__github__/eslintrc' => '.eslintrc',
      self::RELAY_ROOT.'__github__/src/flowconfig' => 'src/.flowconfig',
      self::RELAY_ROOT.'__github__/' => '',
      self::RELAY_ROOT => 'src/',
    };
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'fbsource',
      'githubOrg' => 'facebook',
      'githubProject' => 'relay',
      'sourceRoots' => ImmSet {
        self::RELAY_OSS,

        // Directories which will be removed in the future.
        self::NEW_PLUGIN_ROOT,
        self::RELAY_ROOT,
        self::PLUGIN_ROOT,
      },
    );
  }
}

// Allow require() from unit test
if (isset($argv) && realpath($argv[0]) === realpath(__FILE__)) {
  ShipItRelay::cliMain();
}
