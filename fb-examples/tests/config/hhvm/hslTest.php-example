<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt\Config;

use \Facebook\ShipIt\ {
  ShipItChangeset,
};

final class HhvmHslTest extends FBWWWConfigBaseTest {
  <<__Override>>
  public static function getExamplePathMappings(): ImmMap<string, ?string> {
    return ImmMap {
      'flib/foo' => null,
      'html/foo' => null,
      'flib/core/hack/foo/bar.php' => null,
      'flib/core/hack/lib/foo.php' => 'src/foo.php',
      'flib/core/hack/lib/fb/foo.php' => null,
      'flib/core/hack/lib/legacy/foo.php' => null,
      'flib/core/hack/lib/unreleased/foo.php' => null,
      'flib/core/hack/lib/experimental/foo.php' => null,
      'flib/core/hack/lib/__github__/composer.json' => 'composer.json',
      'flib/core/hack/lib/__github__/dot_hhconfig' => '.hhconfig',
      'flib/core/hack/lib/__tests__/foo.php' => 'tests/foo.php',
      'flib/core/hack/lib/dict/__tests__/bar.php' => 'tests/dict/bar.php',
    };
  }

  public function testConditionalComments(): void {
    $changeset = self::map((new ShipItChangeset())
      ->withDiffs(ImmVector {
        shape(
          'path' => 'flib/core/hack/lib/foo.php',
          'body' =>
            " // @oss-enable: herp();\n".
            " derp(); // @oss-disable\n".
            " \n".
            "+derp(); // @oss-disable\n".
            "+  // @oss-enable:   ? \$x\n",
        ),
      }));
    $this->assertSame(
      " herp(); // @oss-enable\n".
      " // @oss-disable: derp();\n".
      " \n".
      "+// @oss-disable: derp();\n".
      "+    ? \$x // @oss-enable\n",
      $changeset->getDiffs()->at(0)['body'],
    );
  }

  public function testImportConditionalComments(): void {
    $changeset = self::mapImport((new ShipItChangeset())
      ->withDiffs(ImmVector {
        shape(
          'path' => 'src/foo.php',
          'body' =>
            " herp(); // @oss-enable\n".
            " // @oss-disable: derp();\n",
        ),
      }));
    $this->assertSame(
      " // @oss-enable: herp();\n".
      " derp(); // @oss-disable\n",
      $changeset->getDiffs()->at(0)['body'],
    );
  }
}
