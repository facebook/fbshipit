<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt\Config;

use \Facebook\ShipIt\ {
  FBCommonFilters,
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  ShipItChangeset,
  ShipItPathFilters,
};

final class FacebookReactNative extends FBShipItConfig {
  const string ROOT = 'xplat/js/react-native-github/';

  <<__Override>>
  protected static function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace('@/\.github\.flowconfig$@',
            '/.flowconfig', $path),
          )
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace('@/\.github\.flowconfig.android$@',
            '/.flowconfig.android', $path),
          )
      |> parent::defaultProjectFilterChangeset($$, $get_path_mappings);
  }

  <<__Override>>
  protected static function getStripCommonFilesExceptions(): ImmVector<string> {
    // FBCommonFilters defines a common set of files to ignore
    // across all fb projects, such as BUCK files and others.
    // We want to export *all* of the react-native-github folder
    // to GitHub, except for a few specific paths
    // (see getDefaultStrippedFiles()).
    return ImmVector {
      '/.*/',
    };
  }

  <<__Override>>
  protected static function getDefaultStrippedFiles(): ImmVector<string> {
    return ImmVector {
      '@^'.self::ROOT.'BUCK$@',
      '@(^|/)node_modules/@',
      '@(^|/)npm-shrinkwrap.json@',
      '@(^|/)packager/react-packager/src/packager_experimental/@',
      '@(^|/)Libraries/Renderer/fb/@',
    };
  }

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      self::ROOT => '',
    };
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'fbsource',
      'githubOrg' => 'facebook',
      'githubProject' => 'react-native',
    );
  }

  <<__Override>>
  protected static function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace(
            '@(^|/)\.flowconfig$@',
            '\1.github.flowconfig',
            $path,
          ),
        )
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace(
            '@(^|/)\.flowconfig.android$@',
            '\1.github.flowconfig.android',
            $path,
          ),
        )
      |> parent::projectImportChangeset($$, $branch_config);
  }
}
