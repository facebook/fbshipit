<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt\Config;

use \Facebook\ShipIt\ {
  FBSourceBranchConfig,
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  GetPathMappingsFn,
  ShipItChangeset,
  ShipItConditionalLinesFilter,
  ShipItPathFilters,
};

abstract class HhvmHslBase extends FBWWWBase {
  abstract const string ROOT;

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      static::ROOT.'__github__/dot_' => '.',
      static::ROOT.'__github__/' => '',
      static::ROOT => 'src/',
    };
  }

  <<__Override>>
  final protected static function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> \preg_replace('/^\\./', 'dot_', $path),
      )
      |> ShipItConditionalLinesFilter::uncommentLines(
        $$,
        '@oss-disable',
        '//',
      )
      |> ShipItConditionalLinesFilter::commentLines(
        $$,
        '@oss-enable',
        '//',
      )
      |> parent::projectImportChangeset($$, $branch_config);
  }
}
