<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/qbcfel6m
 */
namespace Facebook\ShipIt;

use namespace HH\Lib\Str;
use type Facebook\ShipIt\Config\IShipItConfig;


trait FBShipItCLITrait {

  /** Custom exception handler that repeats the message at the bottom.
   *
   * Some of our scheduling tools report the last line of STDERR; with the
   * default handler, this is something fairly meaningless like '#8 {main}'
   */
  final private static function abortWithException(\Throwable $ex): noreturn {
    $first_line = Str\format(
      "Fatal error: Uncaught exception '%s' with message '%s'",
      \get_class($ex),
      $ex->getMessage(),
    );
    ShipItLogger::err(
      "%s\nStack trace:\n%s\n%s\n",
      $first_line,
      $ex->getTraceAsString(),
      $first_line,
    );
    exit(128);
  }

  abstract public static function cliMain(): void;

  final public static function cliForBinary(
    classname<IShipItConfig> $config_class,
  ): void {
    print('--- Starting '.$config_class."\n");
    $config = $config_class::getBaseConfig();
    $runner = new ShipItPhaseRunner($config, $config_class::getPhases());
    self::executeRunner($runner);
  }

  final public static function cliShipIt(
    ?FBShipItConfigeratorConfig $config = null,
    ?string $_external_branch = null,
  ): void {
    $config_name = $config === null ? '' : $config->getConfigeratorName();
    print('--- Starting ShipIt: '.$config_name."\n");
    $runner = new FBShipItProjectRunner(
      FBShipItCLIAction::SHIP_IT,
      $config,
    );
    self::executeRunner($runner);
  }

  final public static function cliImportIt(
    ?classname<FBShipItConfig> $config_class = null,
    ?string $external_branch = null,
  ): void {
    print('--- Starting ImportIt: '.$config_class."\n");
    $runner = new FBShipItProjectRunner(
      FBShipItCLIAction::IMPORT_IT,
      self::nullOrConfigObject($config_class),
      $external_branch,
    );
    self::executeRunner($runner);
  }

  private static function executeRunner(ShipItPhaseRunner $runner): void {
    try {
      $runner->run();
    } catch (ShipItShellCommandException $e) {
      $retry_causes = keyset[
        'abort: unknown compression type',
        'abort: abandoned transaction found!',
      ];
      foreach ($retry_causes as $cause) {
        if (Str\search($e->getError(), $cause) !== null) {
          // Recover by restarting; the init phase will detect the bad state
          // fix it by deleting and re-cloning the repo.
          ShipItLogger::err("**** HG: TRANSACTION ABORT (t12066068) ****\n");
          ShipItLogger::err("%s\n", $e->getError());
          ShipItLogger::err("**** RESTARTING ****\n");
          $runner->run();
          return;
        }
      }
      throw $e;
    }
  }

  private static function nullOrConfigObject(
    ?classname<FBShipItConfig> $config_class,
  ): ?FBShipItConfig {
    if ($config_class === null) {
      return null;
    }
    return $config_class::getInstance();
  }
}
