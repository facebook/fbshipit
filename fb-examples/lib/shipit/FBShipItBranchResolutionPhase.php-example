<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt;


use \Facebook\ShipIt\Config\{
  FBShipItConfigRegistry,
  RepoArcanistProject,
};

class FBShipItBranchResolutionPhase extends ShipItPhase {

  const type TRepoMetadata = shape(
    'land_branch' => ?string,
    'branches' => ImmVector<string>,
    'internal_repo' => string,
    'arcanist_project' => ?string,
  );

  public function __construct(
    private classname<FBShipItConfig> $configClass,
    protected ?string $externalBranch = null,
    private ?string $repoMetadataFile = null,
  ) {}

  <<__Override>>
  protected function isProjectSpecific(): bool {
    return false;
  }

  <<__Override>>
  public function getReadableName(): string {
    return 'Determine branch configs';
  }

  <<__Override>>
  public function getCLIArguments(): ImmVector<ShipItCLIArgument> {
    return ImmVector {
      shape(
        'long_name' => 'external-branch::',
        'description' => 'Only run the config for one external branch',
        'write' => $x ==> $this->externalBranch = $x,
      ),
      shape(
        'long_name' => 'save-repo-metadata-to::',
        'description' => "List metadata for the repository, and exit.",
        'write' => $x ==> $this->repoMetadataFile = $x,
      ),
    };
  }

  public function getConfigClass(): classname<FBShipItConfig> {
    return $this->configClass;
  }

  public static function getRunner(
    classname<FBShipItConfig> $config_class,
    FBSourceBranchConfig $branch_config,
  ): ShipItPhaseRunner {
    return new ShipItPhaseRunner(
      $config_class::getBaseConfig($branch_config),
      $config_class::getPhases($branch_config),
    );
  }

  public function getBranchConfigs(): ImmVector<FBSourceBranchConfig> {
    $config_class = $this->configClass;
    $branch_configs = $config_class::getBranchConfigs();
    if ($this->externalBranch !== null) {
      $branch_configs = $branch_configs->filter(
        $branch_config ==> $branch_config['external'] == $this->externalBranch,
      );
      invariant(
        $branch_configs->count() > 0,
        'No ShipIt configuration found for external branch: %s',
        $this->externalBranch,
      );
      invariant(
        $branch_configs->count() === 1,
        'Multiple internal configs found for external branch: %s',
        $this->externalBranch,
      );
    }
    return $branch_configs;
  }

  public function getRepoMetadata(): self::TRepoMetadata {
    $config_class = $this->configClass;
    $branch_configs = $this->getBranchConfigs();
    $internal_repo = $config_class::getStaticConfig()['internalRepo'];
    if (
      RepoArcanistProject::getNotInArcanistExceptions()
        ->contains($internal_repo)
    ) {
      $arcanist_project = null;
    } else {
      $arcanist_project =
        RepoArcanistProject::getArcanistProject($internal_repo);
    }
    return shape(
      'land_branch' => $config_class::getLandBranch(),
      'branches' =>
        $branch_configs->map($branch_config ==> $branch_config['external']),
      'internal_repo' => $internal_repo,
      'arcanist_project' => $arcanist_project,
    );
  }

  <<__Override>>
  public function runImpl(ShipItBaseConfig $config): void {
    $config_class = $this->configClass;
    $branch_configs = $this->getBranchConfigs();
    if ($this->repoMetadataFile !== null) {
      \file_put_contents(
        $this->repoMetadataFile,
        \json_encode($this->getRepoMetadata()),
      );
      exit(0);
    }
    foreach ($branch_configs as $branch_config) {
      \printf(
        "--- Running for branch: (%s, %s)\n",
        $branch_config['internal'],
        $branch_config['external'],
      );
      $runner = static::getRunner($config_class, $branch_config);
      $runner->run();
    }
  }
}
