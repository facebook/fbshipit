<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt;

final class BuckUpdateInternalMirrorPhase extends ShipItPhase {
  <<__Override>>
  protected function isProjectSpecific(): bool {
    return true;
  }

  <<__Override>>
  public function getReadableName(): string {
    return 'Update the internal mirror of the GitHub repo';
  }

  <<__Override>>
  final public function getCLIArguments(): ImmVector<ShipItCLIArgument> {
    return ImmVector {
      shape(
        'long_name' => 'skip-sync-internal',
        'description' => "Don't ".\strtolower($this->getReadableName()),
        'write' => $_ ==> $this->skip(),
      ),
    };
  }

  <<__Override>>
  public function runImpl(ShipItBaseConfig $config): void {
    // Remove it remote first in case it's changed, NO_THROW in case git errors
    // because the remote already doesn't exist
/* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $config->getSourcePath(),
      /* stdin = */ null,
      ShipItUtil::DONT_VERBOSE | ShipItUtil::NO_THROW,
      'git',
      'remote',
      'rm',
      'github_local',
    );

    // Long-form because https://github.com/facebook/hhvm/issues/6883
    $exec = function (...$command): void use ($config) {
/* HH_FIXME[4128] Use ShipItShellCommand */
      ShipItUtil::shellExec(
        $config->getSourcePath(),
        /* stdin = */ null,
        ShipItUtil::DONT_VERBOSE,
        ...$command,
      );
    };

    $exec(
      'git',
      'remote',
      'add',
      'github_local',
      $config->getDestinationPath(),
    );
    $exec('git', 'fetch', 'github_local');
    $exec(
      'git',
      'push',
      'origin',
      'github_local/master:refs/heads/github/master',
    );
  }
}
